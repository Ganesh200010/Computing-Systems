// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/String.jack

/**
 * Represents character strings. In addition for constructing and disposing
 * strings, the class features methods for getting and setting individual
 * characters of the string, for erasing the string's last character,
 * for appending a character to the string's end, and more typical
 * string-oriented operations.
 */
class String {
    field Array str;
    field int length;
    field int capacity;

    /** constructs a new empty string with a maximum length of maxLength
     *  and initial length of 0. */
    constructor String new(int maxLength) {
        if (~(maxLength = 0)) {
            let str = Array.new(maxLength);
        } else {        // zero-capacity string
            let str = Array.new(1);
        }
        let length = 0;
        let capacity = maxLength;
        return this;
    }

    /** Disposes this string. */
    method void dispose() {
        do Memory.deAlloc(str);
        return;
    }

    /** Returns the current length of this string. */
    method int length() {
        return length;
    }

    /** Returns the character at the j-th location of this string. */
    method char charAt(int j) {
        return str[j];
    }

    /** Sets the character at the j-th location of this string to c. */
    method void setCharAt(int j, char c) {
        let str[j] = c;
        return;
    }

    /** Appends c to this string's end and returns this string. */
    method String appendChar(char c) {
        let str[length] = c;
        let length = length + 1;
        return this;
    }

    /** Erases the last character from this string. */
    method void eraseLastChar() {
        let str[length-1] = 0;
        let length = length - 1;
        return;
    }

    /** Returns the integer value of this string,
     *  until a non-digit character is detected. */
    method int intValue() {
        var int val;
        var int i;
        var int d;
        var bool sign;
        let val = 0;
        let i = 0;
        let sign = false;
        if (str[i] = 45) {
            let sign = true;
            let i = i + 1;
        }
        while(i < length) {
            if ((str[i] > 47) & (str[i] < 57)) {
                let d = str[i] - 48;
                let val = (val * 10) + d;
                let i = i + 1;
            } else {
                if (sign) {
                    let val = val * (-1);
                }
                return val;
            }
        }
        if (sign) {
            let val = val * (-1);
        }
        return val;
    }

    /** Sets this string to hold a representation of the given value. */
    method void setInt(int val) {
        var int len;
        var int temp;
        var int factor;
        let len = 0;
        let length = 0;             // reset str
        if (val < 0) {
            let len = len + 1;      // the addition one is for '-'
            do appendChar(45);      // '-'
            let val = val * (-1);
        }
        let temp = val;
        let factor = 1;

        // count length
        while((temp / 10) > 0) {
            let len = len + 1;
            let temp = temp / 10;
            let factor = factor * 10;
        }
        let len = len + 1;      // integer has at least one digit

        // set string
        while(factor > 0) {
            do appendChar((val/factor)+48);
            let val = val - (factor * (val / factor));
            let factor = factor / 10;
        }

        return;
    }

    /** Returns the new line character. */
    function char newLine() {
        return 10;
    }

    /** Returns the backspace character. */
    function char backSpace() {
        return 129;
    }

    /** Returns the double quote (") character. */
    function char doubleQuote() {
        return 34;
    }
}